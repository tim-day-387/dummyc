10 REM SELECTION SORT PROGRAM
20 REM RANDOMIZE
30 DIM A(100)
40 FOR I=0 TO 99 STEP 1
50 LET A(I)=INT(RND*1000)
60 NEXT I
70 GOSUB 500
80 FOR I=0 TO 98 STEP 1
90 IF A(I)<=A(I+1) THEN 120
100 PRINT "SORT FAILED"
110 GOTO 140
120 NEXT I
130 PRINT "SORT SUCCEEDED"
140 GOSUB 900
150 STOP
500 REM SELECTION SORT SUBROUTINE
505 REM I1 IS INDEX OF MINIMUM VALUE FOUND SO FAR
510 FOR J=0 TO 98 STEP 1
520 LET I1=J
530 FOR I=J+1 TO 99 STEP 1
540 IF A(I)>=A(I1) THEN 560
550 LET I1=I
560 NEXT I
570 IF I1=J THEN 610
580 LET T=A(J)
590 LET A(J)=A(I1)
600 LET A(I1)=T
610 NEXT J
620 RETURN
900 REM DUMP ARRAY CONTENTS
910 FOR I=0 TO 99 STEP 1
920 PRINT A(I),
930 NEXT I
940 PRINT
950 RETURN
960 END
