10 REM INSERTION SORT PROGRAM
20 REM RANDOMIZE
30 DIM A(100)
40 FOR I=0 TO 99 STEP 1
50 LET A(I)=INT(RND*1000)
60 NEXT I
70 GOSUB 500
80 FOR I=0 TO 98 STEP 1
90 IF A(I)<=A(I+1) THEN 120
100 PRINT "SORT FAILED"
110 GOTO 140
120 NEXT I
130 PRINT "SORT SUCCEEDED"
140 GOSUB 900
150 STOP
500 REM INSERTION SORT SUBROUTINE
510 FOR I=1 TO 99 STEP 1
520 LET X=A(I)
530 LET J=I
540 IF J<1 THEN 590
550 IF A(J-1)<=X THEN 590
560 LET A(J)=A(J-1)
570 LET J=J-1
580 GOTO 540
590 LET A(J)=X
600 NEXT I
610 RETURN
900 REM DUMP ARRAY CONTENTS
910 FOR I=0 TO 99 STEP 1
920 PRINT A(I),
930 NEXT I
940 PRINT
950 RETURN
960 END
